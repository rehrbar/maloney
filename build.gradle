group 'ch.hsr.maloney'
version '1.0-SNAPSHOT'

// Allows to setup gradle wrapper with gradle itself. Run: gradle wrapper
// Files added should be committed to source control (seems to be best practice).
task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

apply plugin: 'java'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile files('sleuthkit/bindings/java/dist/Tsk_DataModel.jar'){
        builtBy 'tsk-dist-SQLite' // Automatically build TSK first
    }
    testCompile group: 'junit', name: 'junit', version: '4.11'

    compile group: 'org.xerial', name: 'sqlite-jdbc', version: '3.8.11.2'
    compile group: 'com.mchange', name: 'c3p0', version: '0.9.5.2'
}

test {
    systemProperty "java.library.path", file("..\\lib")
    exclude "TSK/**"
}

// Adding ant build commands and prefixing them with 'tsk-'
ant.importBuild('sleuthkit/bindings/java/build.xml') { antTargetName ->
    'tsk-' + antTargetName
}