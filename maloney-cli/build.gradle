version '1.0-SNAPSHOT'

task wrapper(type: Wrapper) {
  gradleVersion = '2.13'
  distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

apply plugin: 'java'
apply plugin: 'application'

mainClassName = "ch.hsr.maloney.maloney_cli.Start"

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile project(':maloney')
    // https://mvnrepository.com/artifact/commons-cli/commons-cli
    compile group: 'commons-cli', name: 'commons-cli', version: '1.3.1'
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.7'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.7'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

//create a single Jar with all dependencies
task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Maloney Command Line Interface',
                'Implementation-Version': version,
                'Main-Class': 'ch.hsr.maloney.maloney_cli.Start'
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}


distributions {
    main {
        contents {
            // adding missing library which is required by libtsk_jni
            from("../sleuthkit/tsk/.libs/") {
                include "libtsk.so.13"
                into "lib"
            }
        }
    }
}

startScripts {
    doLast{
        // Modifying the default script to 
        def unixScriptLines = unixScript.readLines()
        unixScriptLines.addAll(unixScriptLines.size()-1, ['# add library path for tsk', 'export LD_LIBRARY_PATH=$APP_HOME/lib/', '\n'])
        unixScript.text = unixScriptLines.join('\n')
    }
}

run {
    if (project.hasProperty("appArgs")) {
        args Eval.me(appArgs)
    }
}